plugins {
    id 'java'
    id "com.palantir.git-version" version "0.13.0"
    id 'org.hidetake.swagger.generator' version '2.18.2'
    id 'project-report'
    id 'maven-publish'
}

def versionLabel(gitInfo) {
    def branch = gitInfo.branchName // all branches are snapshots, only tags get released
    if (branch == null && !"$System.env.CI_COMMIT_BRANCH".isEmpty()) {
        branch = "$System.env.CI_COMMIT_BRANCH" // Gitlab CI checks out a commit, not a branch. Grab the branch name from the env vars it sets
    }
    def tag = gitInfo.lastTag
    // tag is returned as is. Branch may need cleanup
    return (branch == null || branch == "null" || branch.isEmpty()) ? tag : branch.replace("/","-") + "-SNAPSHOT"
}

group 'gov.usbr.wq'
version versionLabel(versionDetails())

repositories {
    mavenCentral()
}

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'merlin-web-client'
            from components.java
        }
    }
    repositories {
        maven {
            credentials {
                username = project.hasProperty('rmaNexusUser') ? "$rmaNexusUser" : "defaultUser"
                password = project.hasProperty('rmaNexusPassword') ? "$rmaNexusPassword" : "defaultPassword"
            }
            afterEvaluate {
                url = version.toString().endsWith("SNAPSHOT") ?
                        "https://artifact.rmanet.app/repository/usbr-component-snapshots/" :
                        "https://artifact.rmanet.app/repository/usbr-component-releases/"
            }
        }
    }
}

dependencies {
    implementation 'com.squareup.okhttp3:okhttp:4.9.2'
    implementation 'com.auth0:java-jwt:3.18.2'

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.11.0'
    implementation 'org.springframework.boot:spring-boot-starter-websocket:2.3.12.RELEASE'
    implementation 'io.swagger:swagger-annotations:1.6.3'
    implementation 'org.springframework.boot:spring-boot-starter-validation:2.5.6'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'

    swaggerCodegen 'io.swagger:swagger-codegen-cli:2.4.23'
}

task generateMyDependenciesReport(type: DependencyReportTask) { depReport ->
depReport.setConfiguration("runtimeClasspath")
}

// https://www.grabdata2.com/merlinwebservice/swagger/ui/index
// https://editor.swagger.io/
swaggerSources {
    merlin {
        inputFile = file("merlin-swagger.yaml")
        code {
            language = 'spring'
            components = ['models']
            configFile = file('java-config.json')

        }
    }
}

// Configure compile task dependency and source
compileJava.dependsOn swaggerSources.merlin.code
sourceSets.main.java.srcDir "${swaggerSources.merlin.code.outputDir}/src/main/java"
sourceSets.main.resources.srcDir "${swaggerSources.merlin.code.outputDir}/src/main/resources"

build.dependsOn generateSwaggerCode

//
//task printSourceSetInformation(){
//    doLast{
//        sourceSets.each { srcSet ->
//            println "["+srcSet.name+"]"
//            print "-->Source directories: "+srcSet.allJava.srcDirs+"\n"
//            print "-->Output directories: "+srcSet.output.classesDirs.files+"\n"
//            println ""
//        }
//    }
//}

/**
 * To help people understand how the versioning
 * interacts with the git repo
 */
task showGit () {
    doFirst {
        def gitInfo = versionDetails()
        println( "Used:    " + getVersion() ) // The select version (the project.version field)
        println( "Tag:     " + gitInfo.lastTag) // actual tag or commit hash(short) if no tag
        println( "Hash:    " + gitInfo.gitHash) // short commit hash
        println( "Branch:  " + gitInfo.branchName) // branch name (null if tag checkout)
        println( "TagBuild:" + gitInfo.isCleanTag) // true if repoi is in dettached head mode(e.g. git checkout <tag>)
        println( "CI_COMMIT_BRANCH: $System.env.CI_COMMIT_BRANCH") // Used by the Gitlab CI runner, since it checks out a commit, not a branch
    }
}

configure(showGit) {
    group = 'help'
    description = 'Shows Git Tag Versioning information'
}
